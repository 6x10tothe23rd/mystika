⎕IO ⎕ML←0 1

C←{1↓+⌿1 0⌽0,0 2⊤⍵}
MA←{C⍣≡⍺+⍵}
MS←{C⍣≡((-⍴⍺)↑1)+⍺+~⍵}
HX←{'0123456789abcdef'[2⊥⍉⍵⍴⍨4,⍨4÷⍨≢,⍵]}
IE←{+/(∨\⍺>⍵)-(∨\⍺<⍵)}

:Namespace SHA
⎕IO ⎕ML←0 1 ⋄  p←##.MA
H←{⍺⍺↑,⊃{⍵ p⍤1⊃Z/(⌽K0{⍺⍵}¨↓W⍺),⊂⍵}/(⌽⊂[1 2]⍺⍺ P ⍵),GH0 ⍺⍺}
P←{w s m b←(32 512 447 64)(64 1024 895 128)(64 1024 895 128)⊃⍨256 384 512⍳⍺
 (⊢⍴⍨16 w,⍨s÷⍨≢)⍵,1,(0⍴⍨s|m-≢⍵),(b⍴2)⊤≢⍵}
H0←{↑{⊃⌽{(1|d)((⊃⌽⍵),⌊d←2×⊃⍵)}⍣64-∘⌊⍨⍵⍬*.5⊣⎕FR←1287}¨⍵}
GP←{(2=+⌿0=X∘.|X)/X←⍳⍵} ⋄ GH0←{H0∆256 H0∆384 H0∆512⌷⍨256 384 512⍳⍵}
H0∆512←H0 GP 20 ⋄ H0∆384←H0 8↓GP 54 ⋄ H0∆256←32↑[1]H0∆512
S←{⊃≠/⍺⌽¨⊂⍵} ⋄ s←{⊃≠/(⍺⌽¨⊂⍵),⊂(⍺⍺⍴0),(-⍺⍺)↓⍵}
S0←¯28 ¯34 ¯39∘S ⋄ S1←¯14 ¯18 ¯41∘S ⋄ s0←¯1 ¯8∘(7 s) ⋄ s1←¯19 ¯61∘(6 s)
M←{{≠/(-⍳3)(∧/2↑⌽)¨⊂⍵}⍤1⍉3↑⍵} ⋄ C←{((4⌷⍵)∧5⌷⍵)≠(6⌷⍵)∧~4⌷⍵}
W←{{⍵⍪⍉⊃p/{(s1 14⌷⍵)(9⌷⍵)(s0 1⌷⍵)(0⌷⍵)}¯16↑⍵}⍣64⊢⍵}
K0←{⊃⌽{(1|d)((⊃⌽⍵),⌊d←2×⊃⍵)}⍣64-∘⌊⍨⍵⍬*÷3⊣⎕FR←1287}¨(2=+⌿0=X∘.|X)/X←⍳410
Z←{z←¯1⊖⍵ ⋄ z[4;]←z[4;]p(t←⊃p/(S1 4⌷⍵)(C⍵),⍺)p 0⌷z
 z[0;]←z[0;]p⊃p/(S0 0⌷⍵)(M⍵)t ⋄ z}
:EndNamespace

XTIMES∆H←{⍵⍴⍨2⍴⍨2⍟⍴⍵}
XTIMES∆E←{(2*⌈2⍟¯1+(⍴⍺)+⍴⍵)↑¨⍺ ⍵}
XTIMES∆U←{×\1,1↓(⍵÷2)⍴¯1*2÷⍵}
XTIMES∆F←{(⍎'⊣/⍺')∇⍣(×m)⊢(+⌿⍵),[m-0.5]⍺×[⍳m←⍬⍴⍴⍴⍺]-⌿⍵}
XTIMES∆FT←{,(XTIMES∆H XTIMES∆U⍴⍵)XTIMES∆F XTIMES∆H ⍵}
XTIMES∆IFT←{(⍴⍵)÷⍨,(XTIMES∆H+XTIMES∆U⍴⍵)XTIMES∆F XTIMES∆H ⍵}
XTIMES∆RC←{(¯1+(⍴⍺)+⍴⍵)↑XTIMES∆IFT⊃×/XTIMES∆FT¨⍺ XTIMES∆E ⍵}
XTIMES∆M←{(+/∧\0=t)↓t←C⍣≡0,⌊0.5+9○⍺ XTIMES∆RC ⍵}

I∆R←{1↓(⍵,0) MS (-1+⍴⍵)↑(-⍴⍺)↓⍺ XTIMES∆M (-⍴⍺)↓(⍵ XTIMES∆M ⍵)}