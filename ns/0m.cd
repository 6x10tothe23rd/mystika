⎕IO ⎕ML←0 1

MA←{1⌷⍉{(0,⍨1↓∧/⍵),⍪≠/⍵}⍣{∧/0=⊣/⍺}(-⍺⍺)2↑⍺,⍪⍵}
HX←{'0123456789abcdef'[2⊥⍉⍵⍴⍨4,⍨4÷⍨≢,⍵]}

:Namespace SHA
⎕IO ⎕ML←0 1 ⋄ bits←256 384 512 ⋄ bw←{32 64 64⊃⍨bits⍳⍵}
H←{l←⍺⍺{⍵((bw⍺⍺)##.MA)⍤1⊃⍺⍺ Z/(⌽(GK0⍺⍺){⍺⍵}¨↓⍺⍺ W ⍺),⊂⍵}
 ⍺⍺↑,⊃l/(⌽⊂[1 2]⍺⍺P⍵),GH0⍺⍺}
P←{w s m b←(32 512 447 64)(64 1024 895 128)(64 1024 895 128)⊃⍨bits⍳⍺
 (⊢⍴⍨16 w,⍨s÷⍨≢)⍵,1,(0⍴⍨s|m-≢⍵),(b⍴2)⊤≢⍵}
H0←{↑{⊃⌽{(1|d)((⊃⌽⍵),⌊d←2×⊃⍵)}⍣64-∘⌊⍨⍵⍬*.5⊣⎕FR←1287}¨⍵}
GP←{(2=+⌿0=X∘.|X)/X←⍳⍵} ⋄ GH0←{H0∆256 H0∆384 H0∆512⌷⍨bits⍳⍵}
H0∆512←H0 GP 20 ⋄ H0∆384←H0 8↓GP 54 ⋄ H0∆256←32↑[1]H0∆512
S←{⊃≠/⍺⌽¨⊂⍵} ⋄ s←{⊃≠/(⍺⌽¨⊂⍵),⊂(⍺⍺⍴0),(-⍺⍺)↓⍵}
M←{{≠/(-⍳3)(∧/2↑⌽)¨⊂⍵}⍤1⍉3↑⍵} ⋄ C←{((4⌷⍵)∧5⌷⍵)≠(6⌷⍵)∧~4⌷⍵}
W←{p←(bw⍺⍺)##.MA ⋄ x←(¯7 ¯18 3)(¯17 ¯19 10) ⋄ y←(¯1 ¯8 7)(¯19 ¯61 6)
 (a b c)(d e f)←(r←bits⍳⍺⍺)⊃(⊂x),2⍴⊂y ⋄ i←r⊃48 64 64
 {⍵⍪⍉⊃p/{(d e(f s)14⌷⍵)(9⌷⍵)(a b(c s)1⌷⍵)(0⌷⍵)}¯16↑⍵}⍣i⊢⍵}
K0←{{⊃⌽{(1|d)((⊃⌽⍵),⌊d←2×⊃⍵)}⍣64-∘⌊⍨⍵⍬*÷3⊣⎕FR←1287}¨GP ⍵}
K0∆256←32↑¨32 K0 313 ⋄ K0∆384←K0∆512←64 K0 410
GK0←{K0∆256 K0∆384 K0∆512⊃⍨bits⍳⍵}
Z←{p←(bw⍺⍺)##.MA ⋄ x←(¯2 ¯13 ¯22)(¯6 ¯11 ¯25) ⋄ y←(¯28 ¯34 ¯39)(¯14 ¯18 ¯41)
 a b←(bits⍳⍺⍺)⊃(⊂x),2⍴⊂y
 z←¯1⊖⍵ ⋄ z[4;]←z[4;]p(t←⊃p/(b S 4⌷⍵)(C⍵),⍺)p 0⌷z
 z[0;]←z[0;]p⊃p/(a S 0⌷⍵)(M⍵)t ⋄ z}
:EndNamespace

XTIMES∆H←{⍵⍴⍨2⍴⍨2⍟⍴⍵}
XTIMES∆E←{(2*⌈2⍟¯1+(⍴⍺)+⍴⍵)↑¨⍺ ⍵}
XTIMES∆U←{×\1,1↓(⍵÷2)⍴¯1*2÷⍵}
XTIMES∆F←{(⍎'⊣/⍺')∇⍣(×m)⊢(+⌿⍵),[m-0.5]⍺×[⍳m←⍬⍴⍴⍴⍺]-⌿⍵}
XTIMES∆FT←{,(XTIMES∆H XTIMES∆U⍴⍵)XTIMES∆F XTIMES∆H ⍵}
XTIMES∆IFT←{(⍴⍵)÷⍨,(XTIMES∆H+XTIMES∆U⍴⍵)XTIMES∆F XTIMES∆H ⍵}
XTIMES∆RC←{(¯1+(⍴⍺)+⍴⍵)↑XTIMES∆IFT⊃×/XTIMES∆FT¨⍺ XTIMES∆E ⍵}
XTIMES∆C←{1↓+⌿1 0⌽0,0 2⊤⍵}
XTIMES∆M←{(+/∧\0=t)↓t←XTIMES∆C⍣≡0,⌊0.5+9○⍺ XTIMES∆RC ⍵}
